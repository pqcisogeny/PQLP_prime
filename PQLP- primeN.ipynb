{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 58,
   "id": "82411fe5",
   "metadata": {},
   "outputs": [],
   "source": [
    "# algorithmic building blocks\n",
    "\n",
    "def generate_prime(sizep):\n",
    "    p = 0\n",
    "    while not is_prime(p) or p % 4 != 3: \n",
    "    # p is congruent to 3 ensures we could use 1728, and N is congruent to 3 ensures R/NR is a field\n",
    "        p = random_prime(2^sizep)\n",
    "    return p\n",
    "\n",
    "p = generate_prime(100) # p is of input size bits \n",
    "N = generate_prime(120) # N is of input size bits\n",
    "\n",
    "K.<a> = NumberField(x^2 + 1)\n",
    "R = K.ring_of_integers()\n",
    "F.<aa> = R.ideal(N).residue_field()\n",
    "red = F.reduction_map()\n",
    "L = F.lift_map()\n",
    "Q.<i,j,k> = QuaternionAlgebra(QQ,-1,-p)\n",
    "ps = floor((log(p))^4)\n",
    "\n",
    "def powersmooth_integer(S): \n",
    "# generates an integer that's ps-powersmooth and bigger than S\n",
    "    pp = 2\n",
    "    pd = 1\n",
    "    while pd < S:\n",
    "        e = floor(log(ps,pp))\n",
    "        e = e if e%2 == 0 else e-1\n",
    "        pd = pd*pp^e\n",
    "        pp = pp.next_prime()\n",
    "    return pd\n",
    "\n",
    "def RepresentInteger_(N,A,B):\n",
    "    S = floor(p*(log(p))^3)\n",
    "    M = powersmooth_integer(S)\n",
    "    m = floor(sqrt(S/p))\n",
    "    for _ in range(m**2):\n",
    "        z = randint(-m, m)\n",
    "        t = randint(-m, m)\n",
    "        M_ = M - p*(t^2+z^2)\n",
    "        if gcd(z^2 + t^2,N) == 1 and gcd(M_,N) == 1 and M_.is_prime()==True and M_%4 == 1:\n",
    "            (x,y) = sum_of_k_squares(2,M_)\n",
    "            C = x+y*a\n",
    "            D = z+t*a\n",
    "            temp = A*B.conjugate()*C*D.conjugate()\n",
    "            im = R(temp).imag()\n",
    "            sq_test = 4*(4*p^2*(A*B*C*D).norm()-((A*C).norm()-p*(A*D).norm())^2)\n",
    "            if kronecker_symbol(sq_test, N) == 1:\n",
    "                break\n",
    "    return C,D\n",
    "\n",
    "def QuaternionDecomposition(N,A,B,C,D):\n",
    "    A=R(A)\n",
    "    B=R(B)\n",
    "    C=R(C)\n",
    "    D=R(D)\n",
    "    #define the quadratic equation of x3 = s+ti, get x3\n",
    "    f0 = A.norm()*(C.norm()-p*D.norm()) + 2*p*(A*B.conjugate()*C*D.conjugate())[0]\n",
    "    f1 = -4*p*(A*B.conjugate()*C*D.conjugate())[1]\n",
    "    f2 = A.norm()*(C.norm()-p*D.norm()) - 2*p*(A*B.conjugate()*C*D.conjugate())[0]\n",
    "    ZN = Zmod(N)\n",
    "    ZNx.<t> = PolynomialRing(ZN)\n",
    "    f = ZN(f0)+ZN(f1)*t+ZN(f2)*t^2 #forcing s to be 1, could be anything nonzero, the equation for s,t is f0s^2+f1st + f2t^2\n",
    "    x3 = red(1+ZZ(f.roots()[0][0])*a)\n",
    "    #solve linear equation in F, in the notes, x,y are used to denote the solutions\n",
    "    temp = (x3.norm()*red(C.conjugate())*red(D.conjugate())*red(p^3)*red(C.norm()+p*D.norm())).inverse()\n",
    "    xx = temp*(red(p^2)*red(A*(D.conjugate())*(D.conjugate()))*x3 - red(p^2)*red(C.conjugate()*D.conjugate()*B)*x3.conjugate())\n",
    "    yy = temp*(red(p)*red(C*C.conjugate()*A)*x3+red(p^2)*red(C.conjugate()*D*B)*x3.conjugate())\n",
    "    #find x1 and x2 such that x1*x2 = y and x1*x2bar = x\n",
    "    u1 = xx[0]\n",
    "    v1 = xx[1]\n",
    "    u2 = yy[0]\n",
    "    v2 = yy[1]\n",
    "    M1 = (u1+u2)/2\n",
    "    M2 = (u1-u2)/2\n",
    "    M3 = (v1+v2)/2\n",
    "    M4 = (v2-v1)/2\n",
    "    s1 = 1\n",
    "    t1 = M1*s1/M3\n",
    "    s2 = M2/s1\n",
    "    t2 = M1/t1\n",
    "    x1 = (t1+s1*aa)\n",
    "    x2 = t2+s2*aa\n",
    "    #lifting x1,x2,x3 to R\n",
    "    x11 = L(x1)\n",
    "    x22 = L(x2)\n",
    "    x33 = L(x3)\n",
    "    #construct a quaternion algebra\n",
    "    Q.<i,j,k> = QuaternionAlgebra(QQ,-1,-p)\n",
    "    sigma0 = A[0]+A[1]*i+(B[0]+B[1]*i)*j\n",
    "    gamma = C[0]+C[1]*i+(D[0]+D[1]*i)*j\n",
    "    alpha1 = (x11[0]+x11[1]*i)*j\n",
    "    alpha2 = (x22[0]+x22[1]*i)*j\n",
    "    alpha3 = (x33[0]+x33[1]*i)*j\n",
    "    return alpha1,alpha2,alpha3\n",
    "\n",
    "def find_small_prime(N):\n",
    "    pp = 2\n",
    "    for _ in range(N):\n",
    "        #print(_)\n",
    "        if kronecker_symbol(pp, N) == -1:\n",
    "            return pp\n",
    "            break\n",
    "        else:\n",
    "            pp = next_prime(pp)\n",
    "\n",
    "def solve_linear(a, b, c, N):\n",
    "    # Generate a random x\n",
    "    x = mod(randint(0, N-1),N)\n",
    "\n",
    "    # Compute y = (c - ax) / b mod N\n",
    "    y = Mod((c - a * x) / b, N)\n",
    "    return x, y\n",
    "\n",
    "def StrongApproximation_ps(N,mu0): #mu0 in Rj, (n(mu0),N)=1\n",
    "    S = p*N^4\n",
    "    t = Q((mu0*j)/(-p))[0]\n",
    "    s = Q((mu0*j)/(-p))[1]\n",
    "    if kronecker_symbol(p*(t^2+s^2), N) == 1:\n",
    "        F = powersmooth_integer(S)\n",
    "    else:\n",
    "        F = powersmooth_integer(S)*find_small_prime(N)\n",
    "    lam = ZZ((mod(F,N)/mod(p*(s^2+t^2),N)).sqrt())\n",
    "    temp = (ZZ(F-lam^2*p*(t^2+s^2)))/N\n",
    "    coex = mod(lam*p*2*t,N)\n",
    "    coey = mod(lam*p*2*s,N)\n",
    "    cont = mod(temp,N)\n",
    "    for _ in range(N):\n",
    "        c,d = solve_linear(coex, coey, cont, N)\n",
    "        temp1 = ZZ((F-p*((ZZ(lam)*ZZ(t) + ZZ(c)*N)^2+(ZZ(lam)*ZZ(s) + ZZ(d)*N)^2)))\n",
    "        M = temp1/(N^2)\n",
    "        if ZZ(M).is_prime()==True and ZZ(M)%4 == 1:\n",
    "            (a,b) = sum_of_k_squares(2,M)\n",
    "            break    \n",
    "    return lam*mu0+N*((ZZ(a)+ZZ(b)*i)+(ZZ(c)+ZZ(d)*i)*j),lam"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 59,
   "id": "e758a688",
   "metadata": {},
   "outputs": [],
   "source": [
    "# given sigma0 in R+Rj such that n(sigma0,N)=1, solve the PQLP for sigma0 when N is a prime and O_0 is chosen to be isomorphic to End(E_1728)\n",
    "# we generate random p,N in the cell above with p congruent to 3 mod 4 to ensure E_1728 is supersingular, and N congruent to 3 mod 4 ensures R/NR is a field\n",
    "\n",
    "def PQLP_O0(N,sigma0): # returns sigma and lambda such that n(sigma) is powersmooth and sigma = lambda*sigma0 mod NO_0\n",
    "    A = R(sigma0[0]+sigma0[1]*a)\n",
    "    B = R(sigma0[2]+sigma0[3]*a)\n",
    "    C,D = RepresentInteger_(N,A,B)\n",
    "    alpha1,alpha2,alpha3 = QuaternionDecomposition(N,A,B,C,D)\n",
    "    gamma1,lambda1 = StrongApproximation_ps(N,alpha1)\n",
    "    #print((gamma1-lambda1*alpha1)/N)\n",
    "    gamma2,lambda2 = StrongApproximation_ps(N,alpha2)\n",
    "    #print(gamma2)\n",
    "    gamma3,lambda3 = StrongApproximation_ps(N,alpha3)\n",
    "    #print(gamma3)\n",
    "    gamma = C[0]+C[1]*i+(D[0]+D[1]*i)*j\n",
    "    return gamma1*gamma*gamma2*gamma*gamma3, lambda1*lambda2*lambda3\n",
    "\n",
    "def verifyPQLP(sigma0,N): # verifies whether sigma = lambda*sigma0 mod NO_0 and compute the factors of the norm of sigma\n",
    "    sigma,lambdaa = PQLP_O0(N,sigma0)\n",
    "    difference = (sigma-lambdaa*(sigma0))\n",
    "    return difference[0]%N == 0 and difference[1]%N == 0 and difference[2]%N == 0 and difference[3]%N == 0 and gcd(lambdaa,N) == 1,factor(sigma[0]^2+sigma[1]^2+p*(sigma[2]^2+sigma[3]^2))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 60,
   "id": "7968e03b",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(438651374275684106757804520773557670787984911480124106017343494798113649540357741808212383191110717781959382802646134594624726525087060085729866621160565507170831251684953495422422569926903241090558586580971079003951466344854722940516782861325011390961606649047166197585727857075294332379459140678894400 - 283383953621514230094943805960252228909907611281114930669690619945400278871525841815435620114713263263015077430431801983320799804449836722194879496191601690296672753955765144065237532298837096747866292880572968865742225464282002820274464900180418021752270090133765843575306384150965692886975722494838400*i + 7046069938812263196953319605870383130324989444844158675483851384378356628357604399420870527995293727771023500377336981076571470593766396842413161580498763346109144802611912143187403904516631974684566277797508886959712728369306130203746803094052504746385967790502612311329724693588212800*j + 23506714204730753486833699932965486623197917272765738166533772228675566852642710488459613113730408339091761067871999772963064566035737224616410749521128246890525784287605074211922085285919132271606626061032667874684507135101646195024623142996305140065443899230096506704266667841399731200*k,\n",
       " 5163300178927333782058293069075387001125344559557464380883346234039265560635083512610778070435160280242)"
      ]
     },
     "execution_count": 60,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "sigma0 = randint(2^100,2^101)+randint(2^100,2^101)*i+(randint(2^100,2^101)+randint(2^100,2^101)*i)*k # define a random sigma0\n",
    "PQLP_O0(N,sigma0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 61,
   "id": "022bc2da",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(True,\n",
       " 2^120 * 3^70 * 5^50 * 7^41 * 11^30 * 13^30 * 17^12 * 19^12 * 23^12 * 29^12 * 31^12 * 37^12 * 41^12 * 43^12 * 47^12 * 53^12 * 59^12 * 61^12 * 67^12 * 71^6 * 73^6 * 79^6 * 83^6 * 89^6 * 97^6 * 101^6 * 103^6 * 107^6 * 109^6 * 113^6 * 127^6 * 131^6)"
      ]
     },
     "execution_count": 61,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "verifyPQLP(sigma0,N)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0c65b38a",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.8",
   "language": "sage",
   "name": "sagemath-9.8"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
